using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Cpp.Grammar;
using Cpp.Grammar.CppGrammarParseTree;
using Nitra;

namespace CppParserPrinter
{
  /// <summary>
  /// Description of Parser.
  /// </summary>
  public class Parser
  {
    parserHost : ParseSession;
    msg : Nitra.ProjectSystem.CompilerMessageList;
    
    NameToCName(n : Name) : CName
    {
      | Name.Id(Identifier = n) => CName.String(n.GetText())
    }
    
    GetModifier(mod : Modifier) : CMod
    {
      | Modifier.Private => CMod.Private()
      | Protected        => CMod.Protected()
      | Public           => CMod.Public()
    }
    
    GetDecl(decl : TypeDecl) : CTypeDecl
    {
      | TypeDecl.ClassDecl(ClassDecl = ClassDecl(Name = n, BaseOptData = b, BodyData = body)) =>
           
               def get_base(b)
               {
                 def mod = GetModifier(b.Modifier);
                 def name = NameToCName(b.BaseClass);
                 (mod, name) :: []
               }
           
              def decls = body.Map(GetDecl);
              def b = b.Map(get_base).WithDefault([]);
              def name = NameToCName(n);
              CTypeDecl.Type(CComplexType.Class(Some(name), b, decls))
    }
    
    GetSpecifier(spec : Specifier) : CSpec
    {
      | Specifier.Typedef  => CSpec.Typedef()
      | Specifier.Const    => CSpec.Const()
      | Specifier.Inline   => CSpec.Inline()
      | Specifier.Extern   => CSpec.Extern()
      | Specifier.Static   => CSpec.Static()
      | Specifier.Virtual  => CSpec.Virtual()
      | Specifier.Volatile => CSpec.Volatile()
      | _                  => null
    }
    
    GetType(t : CppGrammarParseTree.Type) : CType
    {
      | Type.Named(Name = n) => CType.Class(NameToCName(n))
    }
    
    ConvertTree(decl : Declaration) : CTree
    {
      | Declaration.TypeDecl(TypeDecl = td) => 
          match (GetDecl(td))
          {
            | CTypeDecl.Type(c) => c |> CTree.Type
          }
      | Declaration.Decl(Decl = Decl(SpecifiersData = s, Type = t, DeclsData = d)) => 
      def spec = s.Map(GetSpecifier);
      def type = t |> GetType;
      def decls = [];
      CTree.Decl(spec, type, decls)
    }
    
    public Parse(text : string) : CTree
    {
      if (string.IsNullOrWhiteSpace(text)) null else
      {
        def source = SourceSnapshot(text, null : string);
        def result = parserHost.Parse(source);//Main.StateMachine(source, parserHost);
        def ast = result.CreateParseTree();
        if (result.IsSuccess) 
        {
          match (ast)
          {
            | Declaration as a => ConvertTree(a);
            | _                => null
          }
        } else 
        {
          def msg = msg.GetMessages();
          null
          //(null, msg.GetMessages().NToList())
        }
      }
    }
  }
}
