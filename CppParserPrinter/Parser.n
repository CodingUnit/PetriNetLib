using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Cpp.Grammar;
using Cpp.Grammar.CppGrammarParseTree;
using Nitra;

namespace CppParserPrinter
{
  /// <summary>
  /// Description of Parser.
  /// </summary>
  public class Parser
  {
    parserHost : ParseSession;
    msg : Nitra.ProjectSystem.CompilerMessageList;
    
    public this()
    {
      parserHost = ParseSession(Cpp.Grammar.CppGrammar.Declaration, compilerMessages = msg);
    }
    
    NameToCName(n : Name) : CName
    {
      | Name.Id(Identifier = n)        => CName.String(n.IdentifierBodyData)
      | Name.Splicable(Name = n)       => CName.Splicable(n.IdentifierBodyData)
      //| Name.SplicableList(Name = n)   => CName.SplicableList(n.IdentifierBodyData)
      | Name.SplicableType(Name = n)   => CName.SplicableType(n.IdentifierBodyData)
    }
    
    GetModifier(mod : Modifier) : CMod
    {
      | Modifier.Private => CMod.Private()
      | Protected        => CMod.Protected()
      | Public           => CMod.Public()
    }
    
    GetTypeDecl(decl : StructTypeDecl) : CTypeDecl
    {
      | StructTypeDecl.SplicableMembers(SplicableMembers = SplicableMembers(Name = n)) => CTypeDecl.SplicableMembers(n.IdentifierBodyData)
      | StructTypeDecl.StructDecl(StructDecl = d) => GetTypeDecl(d)
      | StructTypeDecl.Decl(Decl = d)             => d |> GetDecl |> CTypeDecl.Decl
    }
    
    GetTypeDecl(decl : StructDecl) : CTypeDecl
    {
       def decls = decl.BodyData.Map(GetTypeDecl);
       def name  = NameToCName(decl.Name);
       CTypeDecl.Type(CComplexType.Struct(Some(name), decls))
    }
    
    GetTypeDecl(decl : TypeDecl) : CTypeDecl
    {
      | TypeDecl.SplicableMembers(SplicableMembers = SplicableMembers(Name = n)) => CTypeDecl.SplicableMembers(n.IdentifierBodyData)
      | TypeDecl.StructDecl(StructDecl = s) => GetTypeDecl(s)
      | TypeDecl.ClassDecl(ClassDecl = ClassDecl(Name = n, BaseOptData = b, BodyData = body)) =>
           
          def get_base(b)
          {
            def mod = GetModifier(b.Modifier);
            def bs = b.BaseClass;
            def name = NameToCName(bs.Name);
            def template = bs.TemplateOptData.Map(x => x.NamesData[0].Map(NameToCName)).WithDefault([]);
            def cl = CClassName(name, template);
            (mod, cl) :: []
          }
           
          def decls = body.Map(GetTypeDecl);
          def b = b.Map(get_base).WithDefault([]);
          def name = NameToCName(n);
          CTypeDecl.Type(CComplexType.Class(Some(name), b, decls))
              
      | TypeDecl.Decl(Decl = d) => GetDecl(d) |> CTypeDecl.Decl
    }
    
    
    GetSpecifier(spec : Specifier) : CSpec
    {
      | Specifier.Typedef  => CSpec.Typedef()
      | Specifier.Const    => CSpec.Const()
      | Specifier.Inline   => CSpec.Inline()
      | Specifier.Extern   => CSpec.Extern()
      | Specifier.Static   => CSpec.Static()
      | Specifier.Virtual  => CSpec.Virtual()
      | Specifier.Volatile => CSpec.Volatile()
      | _                  => null
    }
    
    GetType(t : CppGrammarParseTree.Type) : CType
    {
      | Type.Named(Name = n) => CType.Class(NameToCName(n))
      | Type.Short           => CType.Short()
      | Type.Int             => CType.Int()
      | Type.Long            => CType.Long()
      | Type.UnsignedType(Type = t) => match (t |> GetType)
                                        {
                                          | CType.Int => CType.UInt()
                                          | CType.Char => CType.UChar()
                                          | CType.Long => CType.ULong()
                                          | x          => x
                                        }
      | Type.SignedType(Type = t) => t |> GetType
      | Type.Char            => CType.Char()
    }
    
    GetDecl(t : Decl) : CTree.Decl
    {
      def spec = t.SpecifiersData.Map(GetSpecifier);
      def type = GetType(t.Type);
      def init = t.InitOptData.Map(x => x.ConstExpr |> GetConstExpr);
      def decls = t.DeclsData.Map(GetDecl(_, init));
      CTree.Decl(spec, type, decls)
    }
    
    GetDecl(t : TreeDecl, init : option[CConstExpr]) : CDecl
    {
      match (t)
      {
        | TreeDecl.Id(Name = n)                  => CDecl.Id(n |> NameToCName, init)
        | TreeDecl.Array(Name = n, SizeData = s) => CDecl.Array(n |> NameToCName, init, s.Map(int.Parse))
        | TreeDecl.FuncDecl(Name = n, ParmsData = p) => 
            def parms = p.Map(x => (x.Type |> GetType, x.NameOptData.Map(NameToCName)));
            CDecl.Function(n |> NameToCName, init, parms)
        | TreeDecl.Pointer(Name = n) => CDecl.Pointer(n |> NameToCName, init)
        | _ => null
      }
    }
    
    GetConstExpr(expr : ConstExpr) : CConstExpr
    {
      null
    }
    
    GetEnumDecl(decl : EnumDecl) : CEnumDecl
    {
      | EnumDecl.Option(Name = n, ValueOptData = v) => 
          def name = n |> NameToCName;
          def val = v.Map(x => GetConstExpr(x.ConstExpr));
          CEnumDecl.Option(name, val)
      | EnumDecl.SplicableMembers(SplicableMembers = SplicableMembers(Name = m)) => 
          CEnumDecl.SplicableMembers(m.IdentifierBodyData)
    }
    
    ConvertTree(decl : Declaration) : CTree
    {
      | Declaration.QuotEnum(EnumDecl = e) => CTree.Quotation(CQuotation.EnumDecl(e |> GetEnumDecl))
      | Declaration.QuotType(QuotType = QuotType(QType = TypePtr(SpecifierOptData = s, Type = t))) => 
          def spec = s.Map(x => [GetSpecifier(x)]).WithDefault([]);
          def type = GetType(t) |> CType.Pointer(spec, _, []);
          CTree.Quotation(CQuotation.Type(type))
      | Declaration.QuotType(QuotType = QuotType(QType = Type(Type = t))) => CTree.Quotation(CQuotation.Type(t |> GetType))
      | Declaration.QuotDecl(QuotDecl = QuotDecl.TypeDecl(TypeDecl = t)) => CTree.Quotation(CQuotation.TypeDecl(GetTypeDecl(t)))
      | Declaration.TypeDecl(TypeDecl = td) => 
      
          match (GetTypeDecl(td))
          {
            | CTypeDecl.Type(c) => c |> CTree.Type
            | CTypeDecl.Decl(d) => d
          }
      | Declaration.Decl(Decl = Decl(SpecifiersData = s, Type = t, DeclsData = d, InitOptData = i)) => 
          def spec = s.Map(GetSpecifier);
          def type = t |> GetType;
          def init = i.Map(x => x.ConstExpr |> GetConstExpr);
          def decls = d.Map(GetDecl(_, init));
          CTree.Decl(spec, type, decls)
    }
    
    public Parse(text : string) : CTree
    {
      if (string.IsNullOrWhiteSpace(text)) null else
      {
        def source = SourceSnapshot(text, null : string);
        def result = parserHost.Parse(source);//Main.StateMachine(source, parserHost);
        def ast = result.CreateParseTree();
        if (result.IsSuccess) 
        {
          match (ast)
          {
            | Declaration as a => ConvertTree(a)
            | _                => null
          }
        } else 
        {
          def msg = msg.GetMessages();
          null
          //(null, msg.GetMessages().NToList())
        }
      }
    }
  }
}
