using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler.Parsetree;
using CppParserPrinter;

namespace CppMacro
{
  public macro CppMacro(expr) syntax("cpp", "(", expr, ")")
  {
    CppMacroUtil.Process(expr)
  }
  
  module CppMacroUtil
  {
    Lift(tree : CTree) : PExpr
    {
      | CTree.Quotation(q) =>
         match (q)
         {
           | CQuotation.Type(t)     => Lift(t)
           | CQuotation.TypeDecl(t) => Lift(t)
         }
      | CTree.Type(c) => <[ CTree.Type($(Lift(c)))]>
      | CTree.TypeDecl(s, t) =>
          def spec = s.Map(Lift);
          <[ CTree.TypeDecl([..$spec], $(Lift(t))) ]>
      | CTree.Decl(s, t, d) => 
          def spec = s.Map(Lift);
          def decls = d.Map(Lift);
          <[ CTree.Decl([..$spec], $(Lift(t)), [..$decls]) ]>
    }
    
    Lift(spec : CSpec) : PExpr
    {
      | CSpec.Typedef => <[ CSpec.Typedef() ]>
      | CSpec.Const   => <[ CSpec.Const() ]>
    }
    
    Lift(tree : CConstExpr) : PExpr
    {
      <[]>
    }
    
    Lift(tree : CComplexType) : PExpr
    {
      | CComplexType.Class(b, d) as c => 
          def bas = b.Map((m, n) => <[ ($(Lift(m)), $(Lift(n))) ]>);
          def decls = d.Map(x => <[ $(Lift(x))]> );
          <[ CComplexType.Class($(Lift(c.Name)), [..$bas], [..$decls]) ]>
    }
    
    Lift(name : CName) : PExpr
    {
      | CName.String(s)    => <[ CName.String($(s : string))]>
      | CName.Splicable(s) => <[ CName.String($(s : usesite)) ]>
      | CName.SplicableType(s) => <[ $(s : usesite) ]>
      //| CName.SplicableList(s) => <[]>
    }
    
    Lift(mod : CMod) : PExpr
    {
      | CMod.Private   => <[ CMod.Private ]>
      | CMod.Public    => <[ CMod.Public ]>
      | CMod.Protected => <[ CMod.Protected ]>
    }
    
    Lift(tree : CType) : PExpr
    {
      | CType.Class(n) => <[ CType.Class($(Lift(n)))]>
      | CType.Char     => <[ CType.Char() ]>
      | CType.Int      => <[ CType.Int() ]>
      | CType.Long     => <[ CType.Long() ]>
      | CType.Short    => <[ CType.Short() ]>
      | CType.UInt     => <[ CType.UInt() ]>
      | CType.ULong    => <[ CType.ULong() ]>
      | CType.UShort   => <[ CType.UShort() ]>
      | CType.UChar    => <[ CType.UChar() ]>
      | CType.Pointer(t, r) => 
          def rank = r.Map(x => <[ $(x : int)]>);
          <[ CType.Pointer($(Lift(t)), [..$rank])]>
    }
    
    Lift(tree : CDecl) : PExpr
    {
      def name = <[ $(Lift(tree.name)) ]>;
      def init = tree.init.Map(x => <[ $(Lift(x))]>) |> Lift;
      match (tree)
      {
        | CDecl.Function(p) => 
        
            def get_parms(t, n)
            {
              def n = n.Map(Lift) |> Lift;
              <[ ($(Lift(t)), $n)]>
            }
            
            def parms = p.Map(get_parms);
            <[ CDecl.Function($name, $init, [..$parms]) ]>
        | CDecl.Id       => <[ CDecl.Id($name, $init) ]>
        | CDecl.Array(s) => 
            def s = s.Map(x => <[ $(x : int)]>) |> Lift;
            <[ CDecl.Array($name, $init, $s) ]>
      }
    }
    
    Lift(opt : option[PExpr]) : PExpr
    {
      | Some(o) => <[ Some($o)]>
      | None()  => <[ None() ]>
    }
    
    Lift(tree : CTypeDecl) : PExpr
    {
      | CTypeDecl.Type(c) => <[ CTypeDecl.Type($(Lift(c)))]>
      | CTypeDecl.Decl(d) => <[ CTypeDecl.Decl($(Lift(d)))]>
    }
    
    public Process(expr : PExpr) : PExpr
    {
      match (expr)
      {
        | <[ (..$e) ]>            => assert2(false);Process(e.Head)// e
        | <[ $(str : string) ]> => //if (!(flags is <[]>)) assert2(false) else ();
                                   def parser = Parser();
                                   def tree = parser.Parse(str);
                                   Lift(tree)
      }
    }
  }
}
