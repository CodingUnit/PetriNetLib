using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Macro;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Collections.Immutable;
using CommonLib.Macros;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler;

namespace PetriNetLib
{
  
  /*
  todo:
    - fifo places
    - sorted tokens places
  */
  
  [Record]
  [Lens]
  class Place
  {
    public idx    : int;
    public id     : string;
    public name   : string;
    public colour : ColourSet;
    
    public init_mark : PExpr;
    public tree      : CpnTree.Place;
    public net       : PetriNetProcessor;
      
    public override GetHashCode() : int
    {
      idx
    }
  }
  
  //[Record]
  //class TokenSet
  //{
  //  public num   : int;
  //  public token : Token;
  //}
  
  [Record]
  [Lens]
  class Transition
  { 
    public idx : int;
    public id  : string;
    public name : string;
    public guard : PExpr;
    
    public time_expr : PExpr;
    public code_segment : PExpr;
    public priority : int;
    public net : PetriNetProcessor;
    
    public OutputPlaces : list[Place * Arc]
    {
      [Memoize]
      get
      {
        net.OutputTransitionArcs(this)
      }
    }
    
    public InputPlaces : list[Place * Arc]
    {
      [Memoize]
      get
      {
        net.InputTransitionArcs(this)
      }
    }
    
    public override GetHashCode() : int
    {
      idx
    }
  }
  
  [Record]
  variant ArcSource
  {
    | PlaceEnd      {place : Place}
    | TransitionEnd {tran  : Transition}
  }
  
  
  [Record]
  [Lens]
  class Arc
  { 
    public expr : PExpr;
    
    public from : ArcSource;
    public to   : ArcSource;
  }
  
  [Record]
  class ColourSet
  {
    public name : string;
    public type : Colour;
  }
  
  variant ExecError
  {
    | TokenConstantNotFound { place : Place; val : object }
    | VarNotFound {name : string}
    | TypeTokenNotCompat {incol : Colour; outcol : Colour;place : Place}
  }
  
  [Flags]
  public enum PetriNetFlags
  {
    | Debug = 1
  }
  
  public class PetriNet
  {
    mutable gen : Generator;
    mutable proc : PetriNetProcessor;
    
    [FlagAccessor(
                  Debug
                )]
    public Flags : PetriNetFlags;
    
    public PhaseTwo() : void
    {
      gen.PhaseTwo()
    }
    
    public Process(ty : TypeBuilder, typer : Typer, file : string) : void
    {
      def parser = Parser();
      parser.Parse(file);
      def file = System.IO.Path.GetFileNameWithoutExtension(file);
      proc = PetriNetProcessor(file, typer, ty, parser.Places, parser.Transitions, parser.Arcs, parser.Decls);
      proc.Process();
      gen = Generator(proc.OutModel, proc);
      gen.Generate();
      ty.UserData["PetriNetGen"] = this;
    }
  }
  
  class PetriNetProcessor
  {
    mutable places      : array[Place];
    mutable transitions : array[Transition];
    mutable arcs        : array[Arc];
    public  vars        : Hashtable[string, ColourSet] = Hashtable();
    public  colour_sets : Hashtable[string, ColourSet] = Hashtable();
    [Accessor]
    mutable ml_decls    : list[PExpr];
    name : string;
    tree_places : list[CpnTree.Place];
    tree_tran   : list[CpnTree.Transition];
    tree_arcs   : list[CpnTree.Arc];
    tree_decls  : list[CpnDecl];
    [Accessor]
    typer : Typer;
    [Accessor]
    typebuilder : TypeBuilder;
    
    public OutModel : OutModel
    {
      [Memoize]
      get
      {
        OutModel(places, transitions, arcs, vars, colour_sets, ml_decls)
      }
    }
    
    mutable matrix_len         : array[int];
    mutable input_matrix       : array[2, Arc];//List[List[List[Arc]]];
    mutable output_matrix      : array[2, Arc];
    mutable incidence_matrix   : array[2, int];
    colours            : list[Colour] = [];
    [Accessor]
    converter          : Converter = Converter();
    
    public InputTransitionArcs(tran : Transition) : list[Place * Arc]
    {
      places.MapIndex((p, i) => (p, output_matrix[i, tran.idx])).Filter((_, a) => a != null)
    }
    
    public OutputTransitionArcs(tran : Transition) : list[Place * Arc]
    {
      places.MapIndex((p, i) => (p, input_matrix[i, tran.idx])).Filter((_, a) => a != null)
    }
    
    public InputArcs() : IEnumerable[Place * Transition * Arc]
    {
      foreach (_ in places with p)
      {
        foreach (_ in transitions with t)
        {
          def arc = input_matrix[p, t];
          if (arc != null)
            yield (places[p], transitions[t], arc) else ()
        }
      }
    }

    public OutputArcs() : IEnumerable[Place * Transition * Arc]
    {
      foreach (_ in places with p)
      {
        foreach (_ in transitions with t)
        {
          def arc = output_matrix[p, t];
          when (arc != null)
            yield (places[p], transitions[t], arc)
        }
      }
    }
    
    BuildMatrix() : void
    {
      def places = places.Length;
      def tran   = transitions.Length;
      
      
      when (matrix_len == null || matrix_len[0] != places || matrix_len[1] != tran)
      {
        input_matrix     = array(places, tran);
        output_matrix    = array(places, tran);
        incidence_matrix = array(places, tran);
      }
      
      matrix_len = array[places, tran];
    }
    
    public this(name : string, ty : Typer, typebuilder : TypeBuilder, places : list[CpnTree.Place], tran : list[CpnTree.Transition], arcs : list[CpnTree.Arc], decls : list[CpnDecl])
    {
      this.name = name;
      tree_places = places;
      tree_tran = tran;
      tree_arcs = arcs;
      tree_decls = decls;
      typer = ty;
      this.typebuilder = typebuilder;
    }
    
    public Process() : void
    {
      mutable pidx : int = 0;
      mutable tidx : int = 0;
      
      def create_place(p)
      {
        | CpnTree.Place(n, id, t, i) =>
            def col = colour_sets[t];
            def expr = i |> converter.ParseML;
            def place = Place(pidx, id, n, col, expr, p, this);
            pidx += 1;
            place
      }
      
      def create_tran(t)
      {
        | CpnTree.Transition(n, id, g, t) =>
        
            def guard = g.MapDefault(converter.ParseML, <[]>);
            def time  = t.MapDefault(converter.ParseML, <[]>);
            def tran = Transition(tidx, id, n, guard, time, null, 0, this);
            tidx += 1;
            tran
      }
      
      def create_arcs(a)
      {
        | CpnTree.Arc(t, from, to, p2t) => 
            def expr = converter.ParseML(t);
            def placeend = end => places.First(x => x.id == end) |> ArcSource.PlaceEnd;
            def transend = end => transitions.First(x => x.id == end) |> ArcSource.TransitionEnd;
            def (f, t) = if (p2t) (placeend, transend) else (transend, placeend);
            def from = from |> f;
            def to =   to   |> t;
            Arc(expr, from, to)
      }
      
      def (color, decls) = tree_decls.PartitionType();
      foreach (d : CpnDecl.ColorSet in color)
      {
        //match (d)
        //{
        //  | CpnDecl.ColorSet(n, Colour.Index(id = i)) => colour_sets[i] = ColourSet(i, Colour.Alias(n))
        //  | _                                         => ()
        //}
        colour_sets[d.name] = ColourSet(d.name, d.type)
      }
      
      foreach (d in decls)
      {
        | CpnDecl.Var(i, c) => vars[i] = colour_sets[c]
        | ML(str)           => def expr = converter.ParseML(str);
                               ml_decls ::= expr
        | _                 => ()
      }
      
      places      = tree_places.MapToArray(create_place);
      transitions = tree_tran.MapToArray(create_tran);
      arcs        = tree_arcs.MapToArray(create_arcs);
      BuildMatrix();
      foreach (Arc(_, f, t) as a in arcs with i)
      {
        match (f, t)
        {
          | (ArcSource.PlaceEnd(p), ArcSource.TransitionEnd(t)) => output_matrix[p.idx, t.idx] = a;
          | (ArcSource.TransitionEnd(t), ArcSource.PlaceEnd(p)) => input_matrix[p.idx, t.idx] = a;
          | _ => ()
        }
      }
      //BuildMatrix();
    }
    
    
  }
}
