using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Builtins;
using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using CommonLib.Macros;
using PetriNetModel;
using System.Reactive.Linq;
using System.Threading;
using System.Threading.Tasks;

namespace PetriNetLib
{
  /// <summary>
  /// Description of GenCppImpl.
  /// </summary>
  partial class CppGenerator
  {
        //mutable typing_method : MethodBuilder;
    //compiler : CompilerHost = CompilerHost();
    mutable cons_expr : List[string] = List();
    mutable private_fields    : List[string] = List();
    mutable public_fields    : List[string] = List();
    mutable protected_fields : List[string] = List();
    mutable extern_fields    : List[string] = List();
    mutable first_public_fields : List[string] = List();
    mutable init_body : list[string] = [];
    file : string;
    mutable ctx : list[string];
    
    public AddCtxVar(var : string) : void
    {
      ctx ::= var
    }
    
    public ReadCtxVar() : list[string]
    {
      def res = ctx;
      ctx = [];
      res
    }
    
    main_class : string
    {
      [Memoize]
      get
      {
        
        $<# ..$(extern_fields;"\n")
            class $Name : public $Base
            {
              public:
                ..$(first_public_fields;"\n")
              private:
                  
              ..$(private_fields;"\n")

              protected:
                ..$(protected_fields;"\n")
              public:

              ..$(public_fields;"\n")
            };
        #>
      }
    }

    public this(file : string, gen : Generator, model : OutModel, proc : PetriNetProcessor)
    {
      this.typer = proc.Typer;
      this.file = file;
      this.gen = gen;
      this.typebuilder = proc.Typebuilder;

      this.model = model;
      this.proc = proc;
      this.converter = proc.Converter;
      
      this.places = model.places.MapToArray(GenPlaceCpp(_, this, 0));
      //mutable idx = 0;
      this.transitions = model.transitions.MapToArray(x => GenTranCpp(x, this)).ToArray();
      funcs = model.ml_decls.Where(GenFuncCpp.IsFunc).Map(x => GenFuncCpp(x, this));
      def funcs_wpre = funcs.Select(x => (x.NameWithPrefix, x));
      def funcs_pre = funcs.Where(x => x.PrefixType != FuncPrefix.None).Select(x => (x.Name, x));
      def conc = funcs_wpre.Concat(funcs_pre);
      def dups = conc.GroupBy((n, _) => n);
      def all = dups.SelectMany(x => if (x.Count() == 1) x else x.MapIndex(((x, y), i) => (x + i.ToString(), y)));
      func_proc = Hashtable(all);
      //this.transitions.
    }

    Name : string
    {
      [Memoize]
      get
      {
        proc.Name
      }
    }

    //IsTokenExpr(expr : PExpr) : bool
    //{
    //  | <[ $(_ : usesite) ]> => false
    //  | _                    => true
    //}

    internal GetTokenExpr(expr : PExpr) : string
    {
      def loop(e)
      {
        | <[ $(count : int) ` $expr ]> => Enumerable.Repeat(expr, count).NToList()
        | <[ $left %++ $right ]>       => loop(left) + loop(right)
        | _                            => [expr]
      }

      match (expr)
      {
        | <[$_(..$_)]> as e       => e |> ConvExpr(_, true)
        | <[ $(_ : int)]> as e
        | <[ $(_ : long)]> as e
        | <[ $(_ : short)]> as e
        | <[ $(_ : ushort)]> as e
        | <[ $(_ : byte)]> as e
        | <[ $(_ : string)]> as e => e |> ConvExpr(_, true)
        | e                       => def e = loop(e);
                                    CreateArray(e)//<[array[..$e]]>
      }
    }
    
    internal ConvType(te : TExpr) : string
    {
      te.Type.ToString()
    }

    internal ConvType(te : TypeVar) : string
    {
      def tystr = te.ToString().TrimEnd('-', '+');
      //def enum_ty = typer.BindType(<[ System.Collections.Generic.IEnumerable[_] ]>);
      def list_ty = typer.BindType(<[ list[_] ]>);
      def ty = te.Fix();
      if (te.TryRequire(list_ty))
      {
        def _ty = ty :> FixedType.Class;
        //def args = ty.args;
        //def args = args.Map(ConvType);
        //<#token_iter#>
        //def t = ty.args.Map(ConvType).First();
        <#bytesn#>
      } else
        match (ty)
        {
          | FixedType.Tuple(args) => def vals = args.Map(ConvType);
                                  def n = vals.Length;
                                  $<#tuple$n<..$(vals;",")>#>
          | _                     => tystr
        }
    }

    //ConvFunc(func : string) : string
    //{
    //  if (func.StartsWith("EXT_")) func.Substring(4) else
    //  if (func.StartsWith("FUNC_")) func.Substring(5) else func
    //}

    ConvParm(n : string, t : string) : string
    {
      def col = colours[t];
      def type = col.ParmType;
      $<#$type$n#>;
    }

    internal ConvParm(expr : PParameter) : string
    {
      def deflt = expr.DefaultValue.Map(x => $" = $(ConvExpr(x, false))").WithDefault("");
      //def t = expr.Type;
      //def type = if (IsComplexType(t.ToString())) $<#const $t &#> else $<#$t #>;
      def expr = ConvParm(expr.Name, expr.Type.ToString());
      $<#$expr$deflt#>;
    }

    TranMask(tr : IEnumerable[GenTranCpp]) : string
    {
      def tran = tr.Map(x => $"tr_$(x.ExecName)");
      tran.Fold("", (t, a) => if (a.IsEmpty()) t else $"$t | $a");
    }
    
    Base : string
    {
      [Memoize]
      get
      {
        def s32 = if (places.Filter(x => !x.IsFuncPlace).Length <= 32 && EnumTransitions.Length <= 32) "32" else "";
        if (model.IsTimed) $"timed_petri_net$s32" else $"petri_net$s32"
      }
    }
    
  }
}
