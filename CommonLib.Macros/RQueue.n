using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Threading;

namespace CommonLib.Macros
{
  public class RQueue[T] : IEnumerable[T]
  {
    mutable queue : array[T];
    [Accessor]
    mutable write_index : int;
    [Accessor]
    mutable read_index : int;
    
    public this(size : int) {queue = array(size)}
    
    public Enqueue(elem : T) : void
    {
      _ = ThreadSafeUtils.LockFreeUpdateIntRet(ref write_index, y => {queue[y % queue.Length] = elem; y + 1});
    }
    
    public Resize(size : int) : void
    {
      Array.Resize(ref queue, size);
      _ = ThreadSafeUtils.LockFreeUpdateIntRet(ref write_index, y => y);
    }
    
    public Empty : bool
    {
      get
      {
        Count == 0
      }
    }
    
    public GetEnumerator() : IEnumerator[T] 
    {
      for (mutable i = read_index; i != write_index; i++)
      {
        yield queue[i % queue.Length];
      }
    }
    
    public Clear() : void
    {
      write_index = 0;
      read_index = 0;
    }
    
    public Deque() : T
    {
      mutable val;
      _ = ThreadSafeUtils.LockFreeUpdateIntRet(ref read_index, x => 
      {
        val = queue[x % queue.Length]; 
        queue[x % queue.Length] = default(T);
        x + 1
      });
      val
    }
    
    public Full : bool
    {
      get
      {
        Count == queue.Length
      }
    }
    
    public Count : int
    {
      get
      {
        write_index - read_index
      }
    }
    
    // remove element deque from begin and set to this element
    public Remove(index : int) : T
    {
      def new_elem = Deque();
      def elem = queue[index];
        // из очереди удаляем из начала
      when (index >= read_index)
      {
          // присваиваем новому элементу индекс удаленного
        //new_elem.IndexInItems     = entry.IndexInItems;
          // заменяем элемент удаленный элементом из начала
        //items[entry.IndexInItems] = new_elem;
        queue[index] = new_elem
      }
      elem
    }
    
    public Peek() : T
    {
      queue[read_index % queue.Length]
    }
    
    public ElementAtPos(index : int) : T
    {
      queue[(read_index + index) % queue.Length]
    }
    
    public Item[index : int] : T
    {
      get
      {
        queue[index % queue.Length]
      }
      set
      {
        _ = ThreadSafeUtils.LockFreeUpdateIntRet(ref write_index, x => {queue[index % queue.Length] = value; x});
      }
    }
  }
}
